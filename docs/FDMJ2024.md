# FDMJ2024 规范

## 语法

以下是本项目支持的FDMJ语法规范。

```java
Prog -> MainMethod ClassDeclList
  MainMethod -> public int main '(' ')' '{' VarDeclList StmList '}'
    VarDeclList -> ε | VarDecl VarDeclList
      VarDecl -> class id id ';' // id <=> [a-z_A-Z][a-z_A-Z0-9]*
               | int id ';' | int id '=' Const ';' 
               | int '[' ']' id ';' | int '[' ']' id '=' '{' ConstList '}' ';'
               | float id ';' | float id '=' Const ';' 
               | float '[' ']' id ';' | float '[' ']' id '=' '{' ConstList '}' ';'
        Const -> NUM | '-' NUM // NUM <=> [1-9][0-9]*|0|[1-9][0-9]*\.[0-9]*|0\.[0-9]*|[1-9][0-9]*\.|0\.|\.[0-9]*
        ConstList -> ε | Const ConstRest
          ConstRest -> ε | ',' Const ConstRest
    StmList -> ε | Stm StmList
      Stm -> '{' StmList '}' 
           | if '(' Exp ')' Stm else Stm | if '(' Exp ')' Stm // 检查Exp为int或float 
           | while '(' Exp ')' Stm | while '(' Exp ')' ';' // 检查Exp为int或float 
           | Exp '=' Exp ';' // 检查类型是否兼容，允许upcast，允许int和float之间的隐式类型转换 
           | Exp '[' ']' '=' '{' ExpList '}' ';' // 检查Exp为array，ExpList为int或float
           | Exp '.' id '(' ExpList ')' ';' // 检查Exp为class，Exp有id方法，ExpList匹配方法参数
           | continue ';' | break ';' // 检查在while里
           | return Exp ';' 
           | putnum '(' Exp ')' ';' | putch '(' Exp ')' ';' // 检查Exp为int或float
           | putarray '(' Exp ',' Exp ')' ';' // 检查第一个Exp为int或float，第二个Exp为array
           | starttime '(' ')' ';' | stoptime '(' ')' ';'
      Exp -> NUM // 检查是int还是float
           | true | false // 返回int
           | length '(' Exp ')' // 返回int，检查Exp是array
           | getnum '(' ')' // 返回float
           | getch '(' ')' // 返回int
           | getarray '(' Exp ')' // 返回int，检查Exp是array
           | id // 返回变量类型
           | this // 返回class
           | new int '[' Exp ']' | new float '[' Exp ']' // 返回array，检查Exp是int或float
           | new id '(' ')' // 返回class
           | Exp op Exp | '!' Exp | '-' Exp // 检查Exp是int或float，op当都为int时返回int否则返回float，!返回int，-返回Exp类型
           | '(' Exp ')' | '(' '{' StmList '}' Exp ')' // 返回Exp类型
           | Exp '.' id // 检查Exp为class，检查Exp是否有id，返回id类型
           | Exp '.' id '(' ExpList ')' // 检查Exp为class，检查Exp是否有id，检查ExpList是否匹配参数列表，返回方法类型
           | Exp '[' Exp ']' // 检查第一个Exp为array，第二个Exp为int或float
      ExpList -> ε | Exp ExpRest
        ExpRest -> ε | ',' Exp ExpRest
  ClassDeclList -> ε | ClassDecl ClassDeclList
    ClassDecl -> public class id '{' VarDeclList MethodDeclList '}' 
               | public class id extends id '{' VarDeclList MethodDeclList '}' // 检查继承关系
      MethodDeclList -> ε | MethodDecl MethodDeclList
      MethodDecl -> public Type id '(' FormalList ')' '{' VarDeclList StmList '}'
        Type -> class id | int | int '[' ']' | float | float '[' ']'
          FormalList -> ε | Type id FormalRest
            FormalRest -> ε | ',' Type id FormalRest
```

## 说明

FDMJ2024程序的语义与C和Java的编程语言相似。以下是一些补充说明：

- 语法的根节点是 `Prog`。

- FDMJ2024程序的入口是 `MainMethod`，其中 `main` 方法的签名为 `public int main() {}`。

- 二元操作符 (op) 包括 `+`、`-`、`*`、`/`、`<`、`<=`、`>`、`>=`、`==`、`!=`、`&&`、`||`。

- `CONST` 可以是整数 (`[-]?[1-9][0-9]*|0`) 或者浮点数 (`[1-9][0-9]*\.[0-9]*|0\.[0-9]*|[1-9][0-9]*\.|0\.|\.[0-9]*`)。

- `ID` 是由 `[a-z]`、`[A-Z]`、`[0-9]` 和下划线 `_` 组成的任意长度的字符串，但不能是上述语法中使用的任何关键字，且不能以数字开头。区分大小写。

- 所有数组都在堆内存中。`new` 返回指向堆内存的指针。语句 `id[]={exp1, ..., expn}` 用于在堆内存中初始化一个大小为n的新数组。

- 所有语句按从左到右的顺序执行，包括 escape 表达式中的语句，并且只影响作用域内当前代码之后的语句。

- 布尔二元操作符 `&&` 和 `||` 遵循短路语义。

- 类中的所有变量都被视为 `public` 对象 (如 Java 中定义的那样)。

- 子类继承其父类 (如果有，递归地) 中的所有方法和变量。为简化起见，我们假设子类可以重写其祖先类中声明的方法，但必须具有相同的签名 (即相同类型的参数和返回值，尽管id可能不同)。而子类中声明的变量不能与其任何祖先类中声明的变量重名。